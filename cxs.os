// cxs (check xml schema) подготовка XML схем для
// использования в 1С:Предприятие
//
// Версия 1-01
// 08.03.2020

#Использовать cmdline

Функция main()
	Перем ОК, НЕОК, сТекстОшибки;
	Перем мАргументы, ПарсерКС, соПараметрыКС, сСхема, сПрефикс;
	
	// КОНСТАНТЫ
	ОК = 0;
	НЕОК = 1;
	
	// Разбор аргументов командной строки
	//  -path path - путь к файлу XML схемы
	мАргументы = АргументыКоманднойСтроки;
	Если мАргументы.Количество() = 0 Тогда
		Сообщить("Параметры:
			| -path <path> - путь к обрабатываемому файлу XML схемы"
			, СтатусСообщения.Внимание);
		Возврат НЕОК;
	КонецЕсли;
	
	ПарсерКС = Новый ПарсерАргументовКоманднойСтроки();
	ПарсерКС.ДобавитьИменованныйПараметр("-path");
	Попытка
		соПараметрыКС = ПарсерКС.Разобрать(мАргументы);
	Исключение
		сТекстОшибки = "Ошибка в параметрах: %1
			|
			|Допустимые параметры:
			| -path <path> - путь к обрабатываемому файлу XML схемы";
		сТекстОшибки = СтрШаблон(сТекстОшибки, ОписаниеОшибки());
		Сообщить(сТекстОшибки, СтатусСообщения.Внимание);
		Возврат НЕОК;
	КонецПопытки;
	
	// Прочитать файл XML схемы в переменную
	сСхема = "";
	Если НЕ ПрочитатьФайлСхемы(соПараметрыКС["-path"], сСхема) Тогда
		Возврат НЕОК;
	КонецЕсли;
	
	// Получить префикс соответствующий пространству имен w3.org
	сПрефикс = "";
	Если НЕ ПолучитьПрефикс(сСхема, сПрефикс) Тогда
		Возврат НЕОК;
	КонецЕсли;
	
	// Заменить теги < и </ на <prefix: </prefix:
	Если ТребуетсяВставитьПрефикс(сСхема, сПрефикс) Тогда
		Если НЕ ВставитьПрефикс(сСхема, сПрефикс) Тогда
			Возврат НЕОК;
		КонецЕсли;
	КонецЕсли;
	
	// Проверить XML схему по словарю
	Если НЕ ПроверитьСхемуПоСловарю(сСхема) Тогда
		Возврат НЕОК;
	КонецЕсли;
	
	// Записать данные сСхема в новый файл XML схемы
	Если НЕ ЗаписатьФайлСхемы(соПараметрыКС["-path"], сСхема) Тогда
		Возврат НЕОК;
	КонецЕсли;
	
	Возврат ОК;
КонецФункции

Функция ПрочитатьФайлСхемы(сПутьКФайлу, сСхема)
	Перем бВыполненоБезОшибок, сТекстОшибки;
	Перем оФайл, оТкест;
	
	бВыполненоБезОшибок = Ложь;
	сСхема = "";
	
	Если ПустаяСтрока(сПутьКФайлу) Тогда
		сТекстОшибки = "Не указан путь к файлу XML схемы для чтения
			| Функция: ПрочитатьФайлСхемы";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	оФайл = Новый Файл(сПутьКФайлу);
	Если НЕ оФайл.Существует() Тогда
		сТекстОшибки = "Не найден файл по указанному пути %1
			| Функция: ПрочитатьФайлСхемы";
		сТекстОшибки = СтрШаблон(сТекстОшибки, сПутьКФайлу);
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	оТкест = Новый ЧтениеТекста(сПутьКФайлу, КодировкаТекста.UTF8);
	Попытка
		сСхема = оТкест.Прочитать();
	Исключение
		сТекстОшибки = "Ошибка при чтении файла XML схемы. %1
			| Функция: ПрочитатьФайлСхемы";
		сТекстОшибки = СтрШаблон(сТекстОшибки, ОписаниеОшибки());
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		оТкест.Закрыть();
		Возврат бВыполненоБезОшибок;
	КонецПопытки;
	
	оТкест.Закрыть();
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция ЗаписатьФайлСхемы(сПутьКФайлу, Знач сСхема)
	Перем бВыполненоБезОшибок, сТекстОшибки;
	Перем оТкест, оФайл, сПуть, сИмя, сРасширение, сПутьКФайлуНовый;
	Перем ПРЕФ_НОВЫЙ;
	
	// КОНСТАНТЫ
	ПРЕФ_НОВЫЙ = "_modify";
	
	бВыполненоБезОшибок = Ложь;
	
	Если ПустаяСтрока(сПутьКФайлу) Тогда
		сТекстОшибки = "Не указан путь к файлу XML схемы для записи
			| Функция: ЗаписатьФайлСхемы";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	Если ПустаяСтрока(сСхема) Тогда
		сТекстОшибки = "Нет данных для записи в файл XML схемы
			| Функция: ЗаписатьФайлСхемы";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	оФайл = Новый Файл(сПутьКФайлу);
	Если НЕ оФайл.Существует() Тогда
		сТекстОшибки = "Невозможна запись файла по указанному пути %1
			| Функция: ЗаписатьФайлСхемы";
		сТекстОшибки = СтрШаблон(сТекстОшибки, сПутьКФайлу);
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	сПуть = оФайл.Путь;
	сИмя = оФайл.ИмяБезРасширения + ПРЕФ_НОВЫЙ;
	сРасширение = оФайл.Расширение;
	сПутьКФайлуНовый = ОбъединитьПути(сПуть, сИмя + сРасширение);
	
	оТкест = Новый ЗаписьТекста(сПутьКФайлуНовый, КодировкаТекста.UTF8);
	Попытка
		оТкест.Записать(сСхема);
	Исключение
		сТекстОшибки = "Ошибка при записи файла XML схемы. %1
			| Функция: ЗаписатьФайлСхемы";
		сТекстОшибки = СтрШаблон(сТекстОшибки, ОписаниеОшибки());
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		оТкест.Закрыть();
		Возврат бВыполненоБезОшибок;
	КонецПопытки;
	
	оТкест.Закрыть();
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция ПолучитьПрефикс(сСхема, сПрефикс)
	Перем бВыполненоБезОшибок, сТекстОшибки;
	Перем оРегэкс, коллекцияСовпадений;
	
	бВыполненоБезОшибок = Ложь;
	сПрефикс = "";
	
	Если ПустаяСтрока(сСхема) Тогда
		сТекстОшибки = "Нет данных в файле XML схемы
			| Функция: ПолучитьПрефикс";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	оРегэкс = Новый РегулярноеВыражение("xmlns:?(\w*)=""http:\/\/www\.w3\.org\/2001\/XMLSchema""");
	коллекцияСовпадений = оРегэкс.НайтиСовпадения(сСхема);
	
	Если коллекцияСовпадений.Количество() = 0 Тогда
		сТекстОшибки = "В XML схеме не определено пространство имен w3.org
			| отсутствует объявление xmlns:xsd=""http://www.w3.org/2001/XMLSchema""
			| Функция: ПолучитьПрефикс";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	сПрефикс = коллекцияСовпадений[0].Группы[1].Значение;
	
	Если ПустаяСтрока(сПрефикс) Тогда
		сТекстОшибки = "В XML схеме для пространства имен w3.org не объявлен префикс
			| Функция: ПолучитьПрефикс";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция ВставитьПрефикс(сСхема, сПрефикс)
	Перем бВыполненоБезОшибок, сТекстОшибки;
	Перем оРегэкс;
	
	бВыполненоБезОшибок = Ложь;
	
	Если ПустаяСтрока(сСхема) Тогда
		сТекстОшибки = "Нет данных в файле XML схемы
			| Функция: ВставитьПрефикс";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	Если ПустаяСтрока(сПрефикс) Тогда
		сТекстОшибки = "Не указан префикс для установки в XML схеме
			| Функция: ВставитьПрефикс";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	// Поиск "<х"
	оРегэкс = Новый РегулярноеВыражение("<(\w)");
	сСхема = оРегэкс.Заменить(сСхема, "<" + сПрефикс + ":$1");
	
	// Поиск "</"
	оРегэкс = Новый РегулярноеВыражение("<\/");
	сСхема = оРегэкс.Заменить(сСхема, "</" + сПрефикс + ":");
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция СловарьПроверкиСхемы()
	Перем мДанные;
	
	мДанные = Новый Массив();
	// element name
	мДанные.Добавить("complexType");
	мДанные.Добавить("simpleType");
	мДанные.Добавить("complexContent");
	мДанные.Добавить("simpleContent");
	мДанные.Добавить("anyAttribute");
	мДанные.Добавить("attributeGroup");
	
	// element declared
	мДанные.Добавить("substitutionGroup");
	мДанные.Добавить("maxOccurs");
	мДанные.Добавить("minOccurs");
	мДанные.Добавить("processContents");
	
	// type declared
	мДанные.Добавить("fractionDigits");
	мДанные.Добавить("maxExclusive");
	мДанные.Добавить("maxInclusive");
	мДанные.Добавить("maxLength");
	мДанные.Добавить("minExclusive");
	мДанные.Добавить("minInclusive");
	мДанные.Добавить("minLength");
	мДанные.Добавить("totalDigits");
	мДанные.Добавить("whiteSpace");
	
	// shema
	мДанные.Добавить("targetNamespace");
	мДанные.Добавить("attributeFormDefault");
	мДанные.Добавить("elementFormDefault");
	мДанные.Добавить("blockDefault");
	мДанные.Добавить("finalDefault");
	мДанные.Добавить("schemaLocation");
	
	Возврат мДанные;
КонецФункции

Функция ПроверитьСхемуПоСловарю(сСхема)
	Перем бВыполненоБезОшибок, элементСловаря, оРегэкс;
	
	бВыполненоБезОшибок = Ложь;
	
	Если ПустаяСтрока(сСхема) Тогда
		сТекстОшибки = "Нет данных в файле XML схемы
			| Функция: ПроверитьСхемуПоСловарю";
		Сообщить(сТекстОшибки, СтатусСообщения.Важное);
		Возврат бВыполненоБезОшибок;
	КонецЕсли;
	
	Для каждого элементСловаря Из СловарьПроверкиСхемы() Цикл
		оРегэкс = Новый РегулярноеВыражение(элементСловаря);
		оРегэкс.ИгнорироватьРегистр = Истина;
		сСхема = оРегэкс.Заменить(сСхема, элементСловаря);
	КонецЦикла;
	
	бВыполненоБезОшибок = Истина;
	Возврат бВыполненоБезОшибок;
КонецФункции

Функция ТребуетсяВставитьПрефикс(Знач сСхема, Знач сПрефикс)
	Перем бТребуется;

	бТребуется = Истина;
	
	Если СтрНайти(сСхема, "<" + сПрефикс + ":schema ") Тогда
		бТребуется = Ложь;
	КонецЕсли;

	Возврат бТребуется;
КонецФункции

main();